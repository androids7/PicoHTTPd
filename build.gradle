group 'ir.kasra-sh'
version '0.8.9'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'


sourceCompatibility = 1.7

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
//    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// makes fat jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Pico Http Server',
                'Implementation-Version': version,
                'Main-Class': 'ir.kasra_sh.picohttpd.Test'
    }
    baseName = project.name + '-core'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task run(type: JavaExec) {
    main = 'ir.kasra_sh.picohttpd.Test'
    classpath = sourceSets.main.runtimeClasspath
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

install {
  repositories.mavenInstaller {
    pom.project {
      licenses {
        license {
          name 'MIT License'
          url 'license.txt'
          distribution 'repo'
        }
      }
    }
  }
}
